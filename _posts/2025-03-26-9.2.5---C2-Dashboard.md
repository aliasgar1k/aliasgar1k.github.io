---
title: 9.2.5 - C2 Dashboard
date: 2025-03-26 00:00:00 +/-TTTT
categories:
  - Splunk SIEM Implementation
tags:
  - splunk-siem-implementation
image: ../preview-image.png
media_subpath: /assets/img/Splunk-SIEM-Implementation/2025-03-26-9.2.5---C2-Dashboard/
---

![](2025-03-26-9.2.5---C2-Dashboard-1.png)

In this guide, we will create a Splunk dashboard that monitors and detects potential C2 (Command and Control) activity on your network. The dashboard will include five panels:  
  
1. Column chart: C2 agent detections over time    
2. Pie chart: Top 10 process-initiated network connections  
3. Table: Top 10 process-initiated network connections (filtered internal communication)  
4. Table: Microsoft Defender status by computer  
5. Table: Processes created by powershell, cmd, rundll32  
  
## Build Charts and Tables
   
Before we begin building a dashboard, itâ€™s essential to create meaningful charts and tables that provide a comprehensive insight into the current ongoing situation. These charts and tables can be easily derived from quick search queries. Below are some example queries that will help us gain a quick understanding of the situation at a glance through the dashboard.  
  
### Column Chart: C2 Agent Detections over Time
  
To create a column chart that displays the number of C2 agent detections over time, you can use the following query:  
  
```
index="windows_logs" sourcetype="WinEventLog:Microsoft-Windows-Sysmon/Operational" (OriginalFileName IN ("*apollo*", "*athena*", "*freyja*", "*merlin*", "*thanatos*", "*atlas*", "*arachne*", "*hannibal*", "*xenon*", "*service_wrapper*")) | timechart count as "C2 Agent Detections"
```

This query searches for Sysmon logs with original file names matching known C2 agents. The `timechart` command generates a count of these events over time.  
  
### Bar Chart: Top 10 Process-Initiated Network Connections  
  
To create a bar chart that displays the top 10 process-initiated network connections, you can use the following query:  
  
```
index="windows_logs" sourcetype="WinEventLog:Microsoft-Windows-Sysmon/Operational" EventCode=3 Initiated="true" | top 10 Image
```

This query searches for Sysmon logs with event code 3 (network connections) initiated by a process. The `top` command generates the top 10 results by image (process name).  
  
### Table: Top 10 Process-Initiated Network Connections (Filtered Internal Communication)
  
To create a table that displays the top 10 process-initiated network connections (filtered internal communication), you can use the following query:  
  
```
index="windows_logs" sourcetype="WinEventLog:Microsoft-Windows-Sysmon/Operational" EventCode=3 Initiated="true" 
| rex field=_raw "User:\s+(?<User>[^\r\n]+)" 
| where NOT (DestinationIp = "0:0:0:0:0:0:0:1" OR DestinationIp = "127.0.0.1" OR DestinationIp = "239.255.255.250" OR DestinationIp = "224.0.0.252" OR DestinationIp LIKE "ff%") 
| stats count by Image, DestinationIp, DestinationPort, User
```

This query searches for Sysmon logs with event code 3 (network connections) initiated by a process and filters out internal communication (loopback addresses and multicast addresses). The `stats` command generates a count of these events by image, destination IP, destination port, and user.  
  
### Table: Microsoft Defender Status by Computer 
  
To create a table that displays the Microsoft Defender status by computer, you can use the following query:  
  
```
index="windows_logs" sourcetype="WinEventLog:Microsoft-Windows-Windows Defender/Operational" 
| stats count(eval(EventCode=5001)) as DisabledCount, count(eval(EventCode=5000)) as EnabledCount by ComputerName 
| eval DefenderStatus = if(DisabledCount>0, "Disabled", if(EnabledCount>0, "Enabled", "Unknown")) 
| table ComputerName, DefenderStatus
```

This query searches for Windows Defender logs and generates a count of disabled and enabled events by computer name. The `eval` command determines the defender status based on these counts.  
  
### Table: Processes Created by Powershell, Cmd, Rundll32
  
To create a table that displays the processes created by powershell, cmd, rundll32, you can use the following query:  
  
```
index="windows_logs" sourcetype="WinEventLog:Microsoft-Windows-Sysmon/Operational" EventCode=1 
| where match(ParentImage, "(powershell|cmd|rundll32).*\.exe") | table _time, ProcessId, ParentImage, Image
```

This query searches for Sysmon logs with event code 1 (process creation) and filters out processes created by powershell, cmd, and rundll32.  
  
## Step to Create a Dashboard
  
### Step 1: Create a New Dashboard
  
- Log in to your Splunk instance and navigate to the **Search & Reporting** page.  
- Go to **Dashboard** tab.  
- Click on the **Create Dashboard** button.  
- Enter a title and description for your dashboard.  
- Select the Dashboard Type: **Classic** or **Studio**  
- Choose a layout for your dashboard: **Absolute** or **Grid**  
- Click **Create** to create the dashboard.  

![](2025-03-26-9.2.5---C2-Dashboard-2.png)

### Step 2: Insert Charts and Tables into a Dashboard
  
- Go to **Search and Reporting** and enter a search query.  
- Run the search and view the results.  
- Click on **Save As** and select **Existing Dashboard**.  
- Choose the dashboard you want to add the panel to from the list.    
- Give the panel a title and click **Save to Dashboard**.  

![](2025-03-26-9.2.5---C2-Dashboard-3.png)
  
### Step 3: Add Multiple Panels to the Dashboard
  
- Repeat the process in Step 2 for each new search query you want to add. This will allow you to create multiple panels, each displaying a different search query.  
  
### Step 4: Arrange Panels on the Dashboard

- Go to **Dashboard** tab.    
- Click on **Edit** to edit the dashboard.  
- Use the drag-and-drop feature to arrange the panels in the desired order.  
- Use the resize feature to adjust the size of each panel as needed.  
- Click **Save** to save the changes to the dashboard.  

![](2025-03-26-9.2.5---C2-Dashboard-4.png)

### Step 5: Sync Time Range Across All Panels
  
Lastly, we need to Synchronize Time Range Across All Panels to the Dashboard. This would provide us with Consistency and Flexibility making sure that All panels use the same time range, ensuring data alignment and that Changing the global time range updates all panels automatically.  

- On your dashboard, click **Edit**.    
- For each panel, click the **pencil icon** (edit button) next to the search name under the **Data Source** section.  
- In the **Time Range** dropdown, select **Input**.  
- Under **Input**, choose **Global Time Range (global_time)**.  
- Click **Save** to save the changes to the panel.  
- Use the **Time Picker** in your dashboard to set the global time range to your desired window (e.g., 24-hour real-time window).

![](2025-03-26-9.2.5---C2-Dashboard-5.png)

