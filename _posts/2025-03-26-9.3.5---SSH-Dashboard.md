---
title: 9.3.5 - SSH Dashboard
date: 2025-03-26 00:00:00 +/-TTTT
categories:
  - Splunk SIEM Implementation
tags:
  - splunk-siem-implementation
image: ../preview-image.png
media_subpath: /assets/img/Splunk-SIEM-Implementation/2025-03-26-9.3.5---SSH-Dashboard/
---

![](2025-03-26-9.3.5---SSH-Dashboard-1.png)

Now, we will create a Splunk dashboard that monitors and detects potential SSH brute force activity on your network. The dashboard will include six panels:  
  
- Geo Map: Geolocation of Attack IPs
- Bar Chart: Total Failed SSH Login Attempts  
- Time Chart: Failed SSH Logins Over Time  
- Table: Failed Logins by Source IP  
- Table: Brute Force Detection  
- Pie Chart: Top Users Targeted by Brute Force  
  
## Build Charts and Tables 
  
Before we begin building a dashboard, itâ€™s essential to create meaningful charts and tables that provide a comprehensive insight into the current ongoing situation. These charts and tables can be easily derived from quick search queries. Below are some example queries that will help us gain a quick understanding of the situation at a glance through the dashboard.  
  
### Geo Map: Geolocation of Attack IPs
  
To create a geo map that displays the geolocation of attack IPs, you can use the following query:  
  
```
index="linux_logs" source="/var/log/auth.log" ("Failed password for" OR "Failed password for invalid user")
| rex field=_raw "from\s(?P<SourceIp>\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3})"
| iplocation SourceIp
| stats count by Country
```

This query searches for logs in the "linux_logs" index with a source of "/var/log/auth.log" and containing the phrases "Failed password for" or "Failed password for invalid user". It extracts the source IP from the log message, performs a GeoIP lookup, and then uses the stats command to count the number of events by country.  
  
### Bar Chart: Total Failed SSH Login Attempts
  
To create a bar chart that displays the total number of failed SSH login attempts, you can use the following query:  
  
```
index="linux_logs" source="/var/log/auth.log" ("Failed password for" OR "Failed password for invalid user") 
| rex field=_raw "for\s(?P<user>.*?)\sfrom"
| rex field=_raw "from\s(?P<SourceIp>\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3})"
| stats count as "Total Failed Logins" by SourceIp
| sort - "Total Failed Logins"
```
  
This query searches for logs in the "linux_logs" index with a source of "/var/log/auth.log" and containing the phrases "Failed password for" or "Failed password for invalid user". It extracts the user and source IP from the log message, and then uses the stats command to count the number of events by source IP. The results are sorted in descending order by the total number of failed logins.  
  
### Time Chart: Failed SSH Logins Over Time
  
To create a time chart that displays the number of failed SSH logins over time, you can use the following query:  
  
```
index="linux_logs" source="/var/log/auth.log" ("Failed password for" OR "Failed password for invalid user") 
| timechart count as "Failed Logins"
```

This query searches for logs in the "linux_logs" index with a source of "/var/log/auth.log" and containing the phrases "Failed password for" or "Failed password for invalid user". It uses the timechart command to count the number of events over time.  
  
### Table: Failed Logins by Source IP
  
To create a table that displays the source IPs with the highest number of failed login attempts, you can use the following query:  
  
```
index="linux_logs" source="/var/log/auth.log" ("Failed password for" NOT "Failed password for invalid user") 
| rex field=_raw "for\s(?P<user>.*?)\sfrom"
| rex field=_raw "from\s(?P<SourceIp>\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3})"
| stats count as "Failed Logins" by SourceIp
| sort - "Failed Logins"
| head 20
```

This query searches for logs in the "linux_logs" index with a source of "/var/log/auth.log" and containing the phrase "Failed password for" but not "Failed password for invalid user". It extracts the user and source IP from the log message, and then uses the stats command to count the number of events by source IP. The results are sorted in descending order by the number of failed logins, and the top 20 results are returned.  
  
### Table: Brute Force Detection
  
To create a table that combines both failed and successful attempts to find patterns of brute force attacks, you can use the following query:  
  
```
index="linux_logs" source="/var/log/auth.log" ("Failed password for" OR "Accepted password for") 
| rex field=_raw "for\s(?P<user>.*?)\sfrom"
| rex field=_raw "from\s(?P<SourceIp>\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3})"
| stats count(eval(searchmatch("Failed password for"))) as "Failed", count(eval(searchmatch("Accepted password for"))) as "Successful" by SourceIp, user
| where Failed > 5
```

This query searches for logs in the "linux_logs" index with a source of "/var/log/auth.log" and containing the phrases "Failed password for" or "Accepted password for". It extracts the user and source IP from the log message, and then uses the stats command to count the number of failed and successful login attempts by source IP and user. The results are filtered to only include source IPs with more than 5 failed login attempts.  
  
### Pie Chart: Top Users Targeted by Brute Force
  
To create a pie chart that displays the top users targeted by brute force attacks, you can use the following query:  
  
```
index="linux_logs" source="/var/log/auth.log" ("Failed password for" NOT "Failed password for invalid user") 
| rex field=_raw "for\s(?P<user>.*?)\sfrom"
| rex field=_raw "from\s(?P<SourceIp>\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3})"
| stats count as "Failed Logins" by user
| sort - "Failed Logins"
```

This query searches for logs in the "linux_logs" index with a source of "/var/log/auth.log" and containing the phrase "Failed password for" but not "Failed password for invalid user". It extracts the user and source IP from the log message, and then uses the stats command to count the number of failed login attempts by user. The results are sorted in descending order by the number of failed logins.  
  
## Step to Create a Dashboard
  
### Step 1: Create a New Dashboard  
  
- Log in to your Splunk instance and navigate to the **Search & Reporting** page.  
- Go to **Dashboard** tab.  
- Click on the **Create Dashboard** button.  
- Enter a title and description for your dashboard.  
- Select the Dashboard Type: **Classic** or **Studio**  
- Choose a layout for your dashboard: **Absolute** or **Grid**  
- Click **Create** to create the dashboard.  

![](2025-03-26-9.3.5---SSH-Dashboard-2.png)

### Step 2: Insert Charts and Tables into a Dashboard
  
- Go to **Search and Reporting** and enter a search query.  
- Run the search and view the results.  
- Click on **Save As** and select **Existing Dashboard**.  
- Choose the dashboard you want to add the panel to from the list.  
- Give the panel a title and click **Save to Dashboard**.  

![](2025-03-26-9.3.5---SSH-Dashboard-3.png)

### Step 3: Add Multiple Panels to the Dashboard
  
- Repeat the process in Step 2 for each new search query you want to add. This will allow you to create multiple panels, each displaying a different search query.  
  
### Step 4: Arrange Panels on the Dashboard
  
- Go to **Dashboard** tab.  
- Click on **Edit** to edit the dashboard.  
- Use the drag-and-drop feature to arrange the panels in the desired order.  
- Use the resize feature to adjust the size of each panel as needed.  
- Click **Save** to save the changes to the dashboard.  

![](2025-03-26-9.3.5---SSH-Dashboard-4.png)

### Step 5: Sync Time Range Across All Panels
  
Lastly, we need to Synchronize Time Range Across All Panels to the Dashboard. This would provide us with Consistency and Flexibility making sure that All panels use the same time range, ensuring data alignment and that Changing the global time range updates all panels automatically.  
  
- On your dashboard, click **Edit**.  
- For each panel, click the **pencil icon** (edit button) next to the search name under the **Data Source** section.  
- In the **Time Range** dropdown, select **Input**.  
- Under **Input**, choose **Global Time Range (global_time)**.  
- Click **Save** to save the changes to the panel.  
- Use the **Time Picker** in your dashboard to set the global time range to your desired window (e.g., 24-hour real-time window).

![](2025-03-26-9.3.5---SSH-Dashboard-5.png)

